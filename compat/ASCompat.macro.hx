#if macro
import haxe.macro.Context;
import haxe.macro.Expr;
import haxe.macro.Type;
using haxe.macro.Tools;

class ASCompat {
	static function extractVectorElemType(typecheck:Expr):ComplexType {
		switch typecheck.expr {
			case EParenthesis({expr: ECheckType({expr: EConst(CIdent("_"))}, elementType)}):
				return elementType;
			case _:
				throw new Error("This argument must be an `(_:ElementType)` expression", typecheck.pos);
		}
	}

	static function makeVectorTypeReference(elementType:ComplexType, pos:Position):Expr {
		if (elementType.match(TPath({pack: [], name: "ASAny"}))) {
			elementType = macro : flash.AnyType;
		}
		return macro @:pos(pos) (flash.Vector.typeReference() : Class<flash.Vector<$elementType>>);
	}

	static function vectorClass(typecheck:Expr) { // somehow this :Expr typehint is required, otherwise this function receives `null`, will have to reduce this one
		var elementType = extractVectorElemType(typecheck);
		if (Context.defined("flash")) {
			return makeVectorTypeReference(elementType, Context.currentPos());
		} else {
			Context.warning("Getting a value of a specific Class<Vector<T>> is only supported on Flash and will be `null` on other targets", Context.currentPos());
			return macro null;
		}
	}

	static function asVector(value:Expr, typecheck:Expr) {
		var elementType = extractVectorElemType(typecheck);
		var ctReturn = macro : Null<flash.Vector<$elementType>>;
		if (Context.defined("flash")) {
			var eVectorClass = makeVectorTypeReference(elementType, typecheck.pos);
			return macro @:pos(Context.currentPos()) (flash.Lib.as($value, $eVectorClass) : $ctReturn);
		} else {
			return macro @:pos(Context.currentPos()) (ASCompat._asVector($value) : $ctReturn);
		}

	}

	static function isVector(value:Expr, typecheck:Expr) {
		if (Context.defined("flash")) {
			var eVectorClass = makeVectorTypeReference(extractVectorElemType(typecheck), typecheck.pos);
			return macro @:pos(Context.currentPos()) Std.is($value, $eVectorClass);
		} else {
			return macro @:pos(Context.currentPos()) ASCompat._isVector($value);
		}
	}

	static function setTimeout(closure, delay, arguments:Array<Expr>) {
		if (Context.defined("flash")) {
			var args = [closure, delay].concat(arguments);
			var setTimeoutExpr = macro untyped __global__["flash.utils.setTimeout"];
			return macro @:pos(Context.currentPos()) $setTimeoutExpr($a{args});
		}
		if (Context.defined("js")) {
			var args = [closure, delay].concat(arguments);
			var setTimeoutExpr = macro js.Browser.window.setTimeout;
			return macro @:pos(Context.currentPos()) $setTimeoutExpr($a{args});
		}
		var argsExpr = macro [$a{arguments}];
		return macro @:pos(Context.currentPos()) ASCompat._setTimeoutNative($closure, $delay, $argsExpr);
	}

	static function setInterval(closure, delay, arguments:Array<Expr>) {
		if (Context.defined("flash")) {
			var args = [closure, delay].concat(arguments);
			var setIntervalExpr = macro untyped __global__["flash.utils.setInterval"];
			return macro @:pos(Context.currentPos()) $setIntervalExpr($a{args});
		}
		if (Context.defined("js")) {
			var args = [closure, delay].concat(arguments);
			var setIntervalExpr = macro js.Browser.window.setInterval;
			return macro @:pos(Context.currentPos()) $setIntervalExpr($a{args});
		}
		var argsExpr = macro [$a{arguments}];
		return macro @:pos(Context.currentPos()) ASCompat._setIntervalNative($closure, $delay, $argsExpr);
	}

	static function vectorSpliceAll<T>(a:Expr, startIndex:Expr):Expr {
		var pos = Context.currentPos();
		return macro @:pos(pos) {
			var ___v = $a;
			___v.splice($startIndex, ___v.length);
		};
	}

	static function vectorSplice<T>(a:Expr, startIndex:Expr, deleteCount:Expr, values:Expr):Expr {
		var pos = Context.currentPos();
		if (values == null) {
			return macro @:pos(pos) {
				var ___v = $a;
				___v.splice($startIndex, $deleteCount);
			};
		}
		return macro @:pos(pos) {
			var ___v = $a;
			var ___removed = ___v.splice($startIndex, $deleteCount);
			var ___values = $values;
			if (___values != null) {
				for (___i in 0...___values.length) {
					#if flash
					var ___len = ___v.length;
					___v.length = ___len + 1;
					var ___j = ___len;
					while (___j > $startIndex + ___i) {
						___v[___j] = ___v[___j - 1];
						___j--;
					}
					___v[$startIndex + ___i] = ___values[___i];
					#else
					___v.insertAt($startIndex + ___i, ___values[___i]);
					#end
				}
			}
			___removed;
		};
	}

	static function processNull(e:Expr):Expr {
		var e = Context.typeExpr(e);
		switch e.t {
			case TAbstract(_.toString() => "Null", [actualType]):
				var actualMethod = switch actualType.toString() {
					case "Int" | "UInt": "processNullInt";
					case "Float": "processNullFloat";
					case "Bool": "processNullBool";
					case _: throw new Error("processNull can only be called with Null<Int/UInt/Bool/Float>", e.pos);
				}
				var e = Context.storeTypedExpr(e);
				return macro @:pos(Context.currentPos()) ASCompat.$actualMethod($e);
			case _:
				throw new Error("processNull can only be called with Null<Int/UInt/Bool/Float>", e.pos);
		}
	}

	static inline function textFieldGetXMLText(field:Dynamic, ?beginIndex:Int, ?endIndex:Int):String {
		return "";
	}

	static function typeHasMethod(typePath:String, methodName:String):Bool {
		var t = try Context.getType(typePath) catch (_:Dynamic) return false;
		t = Context.follow(t);
		return switch t {
			case TAbstract(_.get() => abs, _):
				if (abs.impl == null) {
					false;
				} else {
					var found = false;
					for (f in abs.impl.get().statics.get()) {
						if (f.name == methodName) {
							found = true;
							break;
						}
					}
					found;
				}
			case TInst(_.get() => cl, _):
				var found = false;
				while (cl != null && !found) {
					for (f in cl.fields.get()) {
						if (f.name == methodName) {
							found = true;
							break;
						}
					}
					cl = if (cl.superClass != null) cl.superClass.t.get() else null;
				}
				found;
			default:
				false;
		}
	}

	static function setPropertyIsEnumerable(obj:Expr, name:Expr, isEnum:Expr):Expr {
		var pos = Context.currentPos();
		if (Context.defined("flash")) {
			return macro @:pos(pos) untyped $obj.setPropertyIsEnumerable($name, $isEnum);
		}
		if (typeHasMethod("openfl.utils.Object", "setPropertyIsEnumerable")) {
			return macro @:pos(pos) (cast $obj : openfl.utils.Object).setPropertyIsEnumerable($name, $isEnum);
		}
		return macro @:pos(pos) {};
	}
}

class ASArray {
	static function pushMultiple<T>(a:Expr, first:Expr, rest:Array<Expr>):Expr {
		return makeMultipleAppend("push", a, first, rest);
	}

	static function unshiftMultiple<T>(a:Expr, first:Expr, rest:Array<Expr>):Expr {
		return makeMultipleAppend("unshift", a, first, rest);
	}

	public static inline function reverse<T>(a:Array<T>):Array<T> {
		return a;
	}

	public static inline function some<T>(a:Array<T>, callback:(item:T, index:Int, array:Array<T>)->Bool, ?thisObj:Dynamic):Bool {
		return false;
	}

	public static inline function map<T, U>(a:Array<T>, callback:(item:T, index:Int, array:Array<T>)->U, ?thisObj:Dynamic):Array<U> {
		return [];
	}

	static function makeMultipleAppend(methodName:String, object:Expr, firstValue:Expr, rest:Array<Expr>):Expr {
		var pos = Context.currentPos();
		if (methodName == "unshift") {
			var values:Array<Expr> = [firstValue];
			values = values.concat(rest);
			return macro @:pos(pos) {
				var ___arr = $object;
				var ___vals = $a{values};
				var ___i = ___vals.length - 1;
				while (___i >= 0) {
					___arr.unshift(___vals[___i]);
					___i--;
				}
				___arr.length;
			};
		}
		var exprs = [macro @:pos(pos) ___arr.$methodName($firstValue)];
		for (expr in rest) {
			exprs.push(macro @:pos(pos) ___arr.$methodName($expr));
		}
		return macro @:pos(pos) {
			var ___arr = $object;
			$b{exprs};
			___arr.length;
		};
	}
}

class ASVector {
	public static inline function reverse<T>(a:Dynamic):Dynamic {
		return a;
	}

	public static inline function map<T, U>(a:Dynamic, callback:(item:T, index:Int, vector:Dynamic)->U, ?thisObj:Dynamic):Dynamic {
		return a;
	}
}
#end
